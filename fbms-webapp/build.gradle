buildscript {
    ext {
        springBootVersion = '2.0.3.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'java'
apply plugin: 'war'

configurations {
    providedRuntime
}

artifacts {
    archives war
}

dependencies {
    compile project(':fbms-core')

    compile 'org.springframework.boot:spring-boot-starter-cache'
    compile 'org.springframework.boot:spring-boot-starter-data-jpa'
    compile 'org.springframework.boot:spring-boot-starter-hateoas'
    compile 'org.springframework.boot:spring-boot-starter-security'
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile "io.springfox:springfox-swagger2:${springfoxVersion}"
    compile "io.springfox:springfox-swagger-ui:${springfoxVersion}"
    compile "org.jasig.portal:uPortal-soffit-renderer:${uPortalVersion}"
    providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'org.springframework.security:spring-security-test'

    /*
     * TODO:  Remove this dependency.
     *
     * It's not appropriate for components like FBMS to come with database drivers baked-in.  They
     * should be added by implementors, perhaps through an overlay in uPortal-start.  Including them
     * in the Apereo release leads to version conflicts.
     */
    runtime 'org.hsqldb:hsqldb:2.4.1'

    /*
     * TODO:  Remove this one as well, once we're comforatble the migration to Spring Boot 2 is stable
     */
    runtime 'org.springframework.boot:spring-boot-properties-migrator'


    /*
     * Bundle the form-builder UI as a webjar
     *
     * Solution based on this article:  http://www.baeldung.com/maven-webjars.  NOTE:  the
     * org.webjars:webjars-locator part was attempted but does not appear to work.
     */
    compile("org.webjars.npm:uportal__form-builder:${formBuilderVersion}") {
        /*
         * The following are dependencies of form-builder as a Node project, but not (1) not
         * available in Maven Central, and (2) not needed to use form-builder as a web component.
         */
        exclude group: 'org.webjars.npm', module: 'js-tokens'
        exclude group: 'org.webjars.npm', module: 'lightercollective'
        exclude group: 'org.webjars.npm', module: 'regenerator-runtime'
    }
}

/**
 * This custom Gradle task starts the Spring Boot app with spring.jpa.hibernate.ddl-auto=create,
 * which drops and recreates the Hibernate-managed tables.
 */
task bootRunDataInit(type: org.springframework.boot.gradle.tasks.run.BootRun) {
    doFirst() {
        main = 'org.apereo.portal.fbms.ApereoFbmsApplication' // Same as "normal"
        classpath = sourceSets.main.runtimeClasspath
        args = ['--init', '--spring.jpa.hibernate.ddl-auto=create']
    }
}

/**
 * This custom Gradle task starts the Spring Boot app and invokes the Importer, passing the location
 * 'docs/examples.'
 */
task bootRunDataImport(type: org.springframework.boot.gradle.tasks.run.BootRun) {
    doFirst() {
        main = 'org.apereo.portal.fbms.ApereoFbmsApplication' // Same as "normal"
        classpath = sourceSets.main.runtimeClasspath
        args = ['--import', "${rootProject.projectDir}/docs/examples"]
    }
}
